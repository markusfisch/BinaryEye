apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
	namespace "de.markusfisch.android.binaryeye"
	compileSdk sdk_version

	defaultConfig {
		minSdk 9
		targetSdk sdk_version

		versionCode 153
		versionName '1.67.1'
	}

	signingConfigs {
		release {
			keyAlias System.getenv('ANDROID_KEY_ALIAS')
			keyPassword System.getenv('ANDROID_KEY_PASSWORD')
			storePassword System.getenv('ANDROID_STORE_PASSWORD')
			def filePath = System.getenv('ANDROID_KEYFILE')
			storeFile filePath ? file(filePath) : null
		}
	}

	sourceSets {
		main.java.srcDirs += 'src/main/kotlin'
		androidTest.java.srcDirs += 'src/androidTest/kotlin'
		test.java.srcDirs += 'src/test/kotlin'
	}

	buildTypes {
		debug {
			applicationIdSuffix '.debug'
		}

		release {
			proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
			minifyEnabled true
			shrinkResources true
			signingConfig signingConfigs.release
		}
	}

	buildFeatures {
		buildConfig true
	}

	bundle {
		language {
			// To make the app bundle contain all language resources
			// so the in-app language setting works. Another solution
			// would be to use the PlayCore API to download language
			// resources on demand, but it makes much more sense to
			// simply include the ~60 kb than adding fragile code and
			// dependencies.
			enableSplit = false
		}
	}

	// Required since Gradle 8.
	compileOptions {
		sourceCompatibility = JavaVersion.VERSION_1_8
		targetCompatibility = JavaVersion.VERSION_1_8
	}
	kotlinOptions {
		jvmTarget = "1.8"
	}
}

dependencies {
	// Kotlin
	implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
	implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.10.2"

	// Support
	implementation "com.android.support:appcompat-v7:$support_version"
	implementation "com.android.support:design:$support_version"
	implementation "com.android.support:preference-v7:$support_version"
	implementation "com.android.support:preference-v14:$support_version"

	// External
	implementation 'com.github.markusfisch:CameraView:1.10.0'
	implementation 'com.github.markusfisch:ScalingImageView:1.4.1'
	implementation 'com.github.markusfisch:zxing-cpp:v2.3.0.4'

	// Testing
	testImplementation 'junit:junit:4.13.2'
	testImplementation 'org.robolectric:robolectric:4.16'
}
