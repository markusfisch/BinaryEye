apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
	compileSdkVersion sdk_version
	buildToolsVersion build_tools_version

	defaultConfig {
		minSdkVersion 9
		targetSdkVersion sdk_version

		versionCode 87
		versionName '1.46.0'

		// It's recommended to set this value to the lowest API level
		// able to provide all the functionality.
		renderscriptTargetApi 24
		renderscriptSupportModeEnabled true
		renderscriptSupportModeBlasEnabled true

		testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
	}

	signingConfigs {
		config {
			keyAlias System.getenv('ANDROID_KEY_ALIAS')
			keyPassword System.getenv('ANDROID_KEY_PASSWORD')
			storePassword System.getenv('ANDROID_STORE_PASSWORD')
			def filePath = System.getenv('ANDROID_KEYFILE')
			storeFile filePath ? file(filePath) : null
		}
	}

	sourceSets {
		main.java.srcDirs += 'src/main/kotlin'
		androidTest.java.srcDirs += 'src/androidTest/kotlin'
		test.java.srcDirs += 'src/test/kotlin'
	}

	buildTypes {
		debug {
			applicationIdSuffix '.debug'
		}

		release {
			proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
			minifyEnabled true
			shrinkResources true
			signingConfig signingConfigs.config
		}
	}

	bundle {
		language {
			// To make the app bundle contain all language resources
			// so the in-app language setting works. Another solution
			// would be to use the PlayCore API to download language
			// resources on demand, but it makes much more sense to
			// simply include the ~60 kb than adding fragile code and
			// dependencies.
			enableSplit = false
		}
	}

	lintOptions {
		// Because at the time of writing, even the latest version
		// of RenderScript is raising this error no matter if
		// renderscriptTargetApi matches targetSdkVersion.
		disable 'GradleCompatible'
	}
}

dependencies {
	androidTestImplementation('com.android.support.test:runner:1.0.2') {
		exclude module: 'support-annotations'
	}
	androidTestImplementation('com.android.support.test:rules:1.0.2') {
		exclude module: 'support-annotations'
	}
	testImplementation 'junit:junit:4.12'

	implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
	implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.2"
	implementation "com.android.support:appcompat-v7:$support_version"
	implementation "com.android.support:design:$support_version"
	implementation "com.android.support:preference-v7:$support_version"
	implementation "com.android.support:preference-v14:$support_version"
	// Do NOT upgrade ZXing because newer versions won't work on API < 24
	// https://github.com/zxing/zxing/issues/1170
	implementation 'com.google.zxing:core:3.3.3'
	implementation 'com.github.markusfisch:CameraView:1.9.1'
	implementation 'com.github.markusfisch:ScalingImageView:1.3.0'
}
