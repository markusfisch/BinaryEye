buildscript {
	// Since RenderScript can no longer be compiled on macOS Catalina
	// with build tools 28.0.3 because Catalina cannot run 32bit binaries
	// and Google failed to provide a 64bit compiler in 28.0.3.
	// Now, as if that wasn't bad enough, the new build tools 29.0.3 that
	// provide a 64bit compiler produce broken builds on Catalina that
	// cannot invoke `RenderScript.forceCompat()` without crashing.
	// So we need this symbol to build on Catalina but remember to use
	// some Linux to produce release builds.
	ext.is_catalina = {
		if (System.properties['os.name'] == 'Mac OS X') {
			def numbers = System.properties['os.version'].split('\\.')
			if (numbers.length > 1) {
				return numbers[1].toInteger() >= 15
			}
		}
		return false
	}()

	ext {
		kotlin_version = '1.3.72'
		tools_version = '3.6.2'
		build_tools_version = ext.is_catalina ? '29.0.3' : '28.0.3'
		sdk_version = 29
		support_version = '25.3.1'
	}

	repositories {
		google()
		jcenter()
	}

	dependencies {
		classpath "com.android.tools.build:gradle:$tools_version"
		classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
	}
}

allprojects {
	repositories {
		google()
		jcenter()
		maven { url 'https://jitpack.io' }
	}

	gradle.projectsEvaluated {
		tasks.withType(JavaCompile) {
			options.compilerArgs << "-Xlint:unchecked"
		}
	}
}

task clean(type: Delete) {
	delete rootProject.buildDir
}
